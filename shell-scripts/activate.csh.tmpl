# This file must be used with "source bin/activate.csh" *from csh*.
# You cannot run it directly.

set newline='\
'

alias eden_deactivate 'test $?_OLD_EDEN_PATH != 0 && setenv PATH "$_OLD_EDEN_PATH:q" && unset _OLD_EDEN_PATH; rehash; test $?_OLD_EDEN_PROMPT != 0 && set prompt="$_OLD_EDEN_PROMPT:q" && unset _OLD_EDEN_PROMPT; unsetenv EDEN_HOME; test "\!:*" != "nondestructive" && unalias eden_deactivate && unalias eden_config && unalias eden+config && unalias eden-config'

alias eden_config 'eden config set \!:1 && set prompt="EDEN-`eden config get`_$_OLD_EDEN_PROMPT:q"'

alias eden+config 'cd `eden config get --key eden.root`/..; eden config add \!:1; cd -'
alias eden-config 'eden config delete \!:1; eden_config default'

# Unset irrelevant variables.
eden_deactivate nondestructive

setenv EDEN_HOME "{{.Eden.Root}}"

set _OLD_EDEN_PATH="$PATH:q"
setenv PATH "{{.Eden.BinDir}}:$PATH:q"

if ( $?EDEN_DISABLE_PROMPT ) then
    if ( $EDEN_DISABLE_PROMPT == "" ) then
        set do_prompt = "1"
    else
        set do_prompt = "0"
    endif
else
    set do_prompt = "1"
endif

if ( $do_prompt == "1" ) then
    # Could be in a non-interactive environment,
    # in which case, $prompt is undefined and we wouldn't
    # care about the prompt anyway.
    if ( $?prompt ) then
        set _OLD_EDEN_PROMPT="$prompt:q"
        if ( "$prompt:q" =~ *"$newline:q"* ) then
            :
        else
            set prompt = "eden-`eden config get`_$prompt:q"
        endif
    endif
endif

unset do_prompt

rehash
